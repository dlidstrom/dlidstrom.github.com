#!fsharp

#r "nuget: Plotly.NET.Interactive, 3.0.0"
#r "nuget: Plotly.NET.ImageExport, 3.0.0"

open System.IO
open System.Text.RegularExpressions
open Plotly.NET
open Plotly.NET.ImageExport

type BetRow = {
    Name : string
    Guesses : string list
    Winner : string
}

type ScoredRow = {
    Name : string
    Scores : float list
    Guesses : string list
}

type ScoredChart = {
    Sum: float
    Name: string
}

let results =
    "resultat.txt"
    |> File.ReadAllLines
    |> Array.map (fun s -> string s[0])
    |> Array.where (fun s -> s.Length > 0)
    |> List.ofArray
let matches =
    "matcher.txt"
    |> File.ReadAllLines
    |> List.ofArray
let betRows =
    let createBetRow (a: string array) = {
        Name = a[2]
        Winner = a[51]
        Guesses = a[3..50] |> List.ofArray
    }
    "tipset.2022.txt"
    |> File.ReadAllLines
    |> Seq.where (fun l -> l.StartsWith('#') = false)
    |> Seq.map (fun l -> Regex.Split(l, @"\s+"))
    |> Seq.map createBetRow
    |> Seq.toList
let countCorrect i r =
    let count =
        betRows
        |> Seq.where (fun br -> br.Guesses[i] = r)
        |> Seq.length
        |> double
    let score =
        if count > 0 then 6. / count
        else 0
    score, r
let correctPerGame =
    results |> List.mapi countCorrect

let scoreGuesses (guesses: string list) =
    let scoreGame i (fs: float * string) =
        let f, s = fs
        if s = guesses[i] then f
        else 0
    correctPerGame
    |> List.mapi scoreGame
let scoredRows =
    let convertGuesses guesses =
        let scores = scoreGuesses guesses
        scores |> List.scan (+) 0.
    let createScoredRow (br: BetRow) =
        { Name = br.Name
          Scores = convertGuesses br.Guesses
          Guesses = br.Guesses }
    betRows
    |> List.map createScoredRow

let createChart (sr: ScoredRow) =
    let xData = [0..results.Length]
    let sum = sr.Scores[results.Length]
    let chart = Chart.Spline(
        xData,
        sr.Scores,
        Name = $"%s{sr.Name} %.1f{sum}",
        Smoothing = 0.6,
        ShowMarkers = true,
        MultiText = [""] @ (sr.Guesses[0..results.Length - 1]),
        TextPosition = StyleParam.TextPosition.BottomCenter,
        ShowLegend = true)
    sum, chart
let description =
    let createHeading name (gs: string list) = seq {
        yield "<div style='margin: 0; padding: 0;'>"
        yield $"<span>%s{name}</span>"
        yield "<ul style='display: inline-block; margin: 0; padding: 0;'>"
        for g in gs do
            yield $"<li style='display: inline-block; border: 1px solid; margin: 2px; padding: 2px;'>%s{g}</li>"
        yield "</ul>"
        yield "</div>"
    }
    let nextGuesses =
        scoredRows
        |> List.map (fun sr -> sr.Name, sr.Guesses[results.Length..results.Length + 3])
        |> List.map (fun (n, gs) -> createHeading n gs)
    let builder = StringBuilder()
    builder.AppendLine("<div style='float: left; margin-right: 2rem;'>") |> ignore
    for m in matches[results.Length..results.Length + 3] do
        builder.AppendLine($"<h4>%s{m}</h4>") |> ignore
    builder.AppendLine("</div>") |> ignore
    builder.AppendLine("<div>") |> ignore
    for x in nextGuesses do
        builder.AppendLine(String.concat "" x) |> ignore
    builder.AppendLine("</div>") |> ignore
    ChartDescription.create "<h3 style='margin:0'>Kommande matcher och v√•ra tips</h3>" (builder.ToString())
let myFirstChart =
    Chart.combine(
        scoredRows
        |> List.map createChart
        |> List.sortByDescending fst
        |> List.map snd
    )
    |> Chart.withDescription(description)
myFirstChart |> Chart.savePNG("chart", Width = 1400, Height = 1000)
myFirstChart
